plugins {
  id 'com.jfrog.bintray' version '1.7.3'
}
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'

def packageVersion = {
  def versionFile = sourceSets.main.resources.srcDirs[0].path + '/' + project.name + '.version'
  def version = new BufferedReader(new FileReader(versionFile)).readLine().trim()
  return version
}()

group = 'com.obsidiandynamics.indigo'
version = packageVersion

def packageName = 'indigo-core'

repositories {
  jcenter()
}

dependencies {
  compile project(':indigo-commons')
  testCompile project(':indigo-assurance')
}

test {
  include 'com/obsidiandynamics/indigo/CycledPrimaryTests.class'
  include 'com/obsidiandynamics/indigo/SupportingTests.class'
}

task testJar(type: Jar) {
  dependsOn jar
  manifest { 
    attributes "Main-Class": "com.obsidiandynamics.indigo.PrimaryTestsSoak"
  }
  from sourceSets.main.output
  from sourceSets.test.output
  from { // bundle all dependencies
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) }
  }   
  baseName packageName + '-test'
}

jar {
  dependsOn ':indigo-commons:jar'
  from {
    configurations.compile.filter({it.name =~ /indigo-commons.jar/}).collect { 
    it.isDirectory() ? it : zipTree(it) }
  }
  baseName packageName
  finalizedBy jacocoTestReport
}

jacoco {
  toolVersion = "0.7.9"
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.enabled false
    html.enabled true
  }
}

def perfJvmArgs = '-server -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark -XX:-UseBiasedLocking -Xms1024M -Xmx2048M -Xss1M -XX:+UseParallelGC'

task testBench(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  jvmArgs = Arrays.asList perfJvmArgs.split(' ')
  main = 'com.obsidiandynamics.indigo.BenchmarkSuite'
}

task testSoak(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  jvmArgs = Arrays.asList perfJvmArgs.split(' ') + '-ea'
  standardInput = System.in
  main = 'com.obsidiandynamics.indigo.PrimaryTestsSoak'
}

def envKey = 'BINTRAY_KEY'

task bintrayCredentialsCheck {
  doLast {
    if (System.getenv(envKey) == null) {
      throw new GradleException("No Bintray key specified; set with 'export ${envKey}=<key>'")
    }
  }
}

bintray {
  user = 'ekoutanov'
  key = System.getenv(envKey)
  publications = ['mavenJava']
  pkg {
    repo = 'indigo'
    name = packageName
    userOrg = 'obsidiandynamics'
    desc = 'A dynamic actor framework for the JVM'
    websiteUrl = 'https://github.com/obsidiandynamics/indigo'
    licenses = ['BSD New']
    vcsUrl = 'https://github.com/obsidiandynamics/indigo.git'
    publicDownloadNumbers = true
    githubRepo = 'obsidiandynamics/indigo'
    override = true
    publish = false
    version {
      name = packageVersion
      desc = 'A dynamic actor framework for the JVM'
      released  = new Date()
      vcsTag = packageVersion
    }
  }
}

// invoke this task when ready to publish to Bintray
bintrayUpload {
  dependsOn bintrayCredentialsCheck
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = 'javadoc'
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = 'sources'
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId group
      artifactId packageName
      version packageVersion
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}